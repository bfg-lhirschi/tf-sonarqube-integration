# Write Github secrets to Java repos
/* NOT FUNCTIONAL
resource "github_actions_organization_secret" "sonar_token" {
  plaintext_value         = var.sonar_token #This value is a protected TFC var
  secret_name             = "SONAR_TOKEN"
  selected_repository_ids = local.java_repo_ids[*].*.repo_id
  visibility              = "selected"
}
*/

# This works but we should be able to use an organizational secret instead. It will reduce the amount of resources being managed.
resource "github_actions_secret" "sonar_token" {
  repository  = var.repo
  secret_name = "SONAR_TOKEN"
  # Optionally use 'ecrypted_value' instead
  plaintext_value = var.sonar_token
}

resource "github_actions_secret" "sonar_host_url" {
  repository  = var.repo
  secret_name = "SONAR_HOST_URL"
  # Optionally use 'ecrypted_value' instead
  plaintext_value = var.sonar_host_url
}

#----------
# Create, commit and open PR to merge on default branch
resource "github_branch" "sonar_branch" {
  branch     = var.sonar_branch
  repository = var.repo
  source_branch = var.default_branch
  lifecycle {
    ignore_changes = [
    etag,
    ]
  }
}

resource "github_repository_file" "sonar_properties" {
  repository = var.repo
  branch     = github_branch.sonar_branch.branch
  file       = "sonar-project.properties"
  content = templatefile("${path.module}/sonar-properties.template", {
    project_name = var.repo,
    project_key = var.repo,
  })
  commit_message      = "Create sonarqube.properties file, managed by Terraform"
  commit_author       = "BFG-TF"
  commit_email        = "bfg-tf@bigfishgames.com"
  overwrite_on_create = true

  lifecycle {
    ignore_changes = all
  }
}

resource "github_repository_file" "sonar_action" {
  repository          = var.repo
  branch              = github_branch.sonar_branch.branch
  file                = ".github/workflows/${var.action_file}"
  content = templatefile(var.action_file, {
    default_branch        = var.default_branch,
    github_hash_files     = var.github_hash_files,
    github_runner_os      = var.github_runner_os,
    github_token          = "$${{ secrets.GITHUB_TOKEN }}"
    java_build_cache_path = local.java_build_cache_path
    java_build_run        = local.java_build_run
    java_build_tool       = local.java_build_tool
    java_permissions      = local.java_permissions
    sonar_host_url        = "$${{ secrets.SONAR_HOST_URL }}"
    sonar_token           = "$${{ secrets.SONAR_TOKEN }}"
  })
  commit_message      = "Create sonarqube GH Action file, managed by Terraform"
  commit_author       = "bfg-tf"
  commit_email        = "bfg-tf@bigfishgames.com"
  overwrite_on_create = true

#  lifecycle {
#    ignore_changes = all
#  }
}

# There is currently a cyclical dep. problem when determining if the files have changes
locals {
  java_build_tool = lower(var.java_build_tool)
  java_build_cache_path = local.java_build_tool == "gradle" ? "~/.gradle/caches" : (local.java_build_tool == "maven" ? "~/.m2" : null)
  java_build_run   = local.java_build_tool == "gradle" ? "./gradlew build sonarqube -Dsonar.host.url=$${{ secrets.SONAR_HOST_URL }} -Dsonar.login=$${{ secrets.SONAR_TOKEN }}" : (local.java_build_tool == "maven" ? "mvn -B verify sonar:sonar -Dsonar.projectKey=redemption-service -Dsonar.host.url=$SONAR_HOST_URL -Dsonar.login=$SONAR_TOKEN -Denv.SHA=$${GITHUB_SHA::7} -Denv.TO=$NONPROD_CONTAINER_IMAGE" : null)
  java_permissions = local.java_build_tool == "gradle" ? "chmod +x ./gradlew" : "echo No Maven file permission changes needed" 
}

resource "github_repository_pull_request" "sonar_pr" {
  base_repository = var.repo
  base_ref        = var.default_branch 
  head_ref        = github_branch.sonar_branch.branch
  title           = "Sonarqube Static Code Analysis Implementation"
  body            = "This PR was generated by Terraform to enable Sonarqube static code analysis on selected repos."

  # The following files must be created before the PR is created
  depends_on = [
    github_repository_file.sonar_properties,
    github_repository_file.sonar_action,
  ]

  lifecycle {
    ignore_changes = [
    head_sha,
    state,
    updated_at,
    ]
  }
}
